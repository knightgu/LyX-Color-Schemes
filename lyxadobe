#!/bin/bash
FILE=$1
PDFFILE="$(pwd)/${FILE}"
osascript <<EOF
    set FilePath to quoted form of POSIX path of "$PDFFILE"
    set TheTitle to do shell script "basename " & FilePath
    set NumPages to (do shell script "/opt/local/bin/qpdf --show-npages " & FilePath)
    set FileNotOpened to false
    tell application "System Events"
      if (exists of application process "Acrobat Reader") then
        tell process "Acrobat Reader"
          set frontmost to true
          try
            if window TheTitle exists then
              tell window TheTitle to perform action "AXRaise"
              if NumPages is greater than 1 then
                keystroke "n" using {shift down, command down}
                keystroke "c" using {command down}
                keystroke tab
                keystroke return
              end if
              tell window TheTitle to perform action "AXPress" of (get value of attribute "AXCloseButton")
            else
              set FileNotOpened to true
            end if
          on error ErrMsg number ErrNum
            display dialog ErrMsg & ": code " & ErrNum
          end try
        end tell
      else
        set FileNotOpened to true
      end if
    end tell
    do shell script "open -a \"Adobe Acrobat Reader DC\" " & FilePath
    tell application "System Events"
      tell process "Acrobat Reader"
        set frontmost to true
        if window TheTitle exists then
          tell window TheTitle to perform action "AXRaise"
          if NumPages is greater than 1 then
            if FileNotOpened then
              set the clipboard to "1"
            end if
            keystroke "n" using {shift down, command down}
            keystroke "v" using {command down}
            keystroke return
          end if
          -- keystroke "2" using {command down}
        else
          display dialog "Cannot open file " & TheTitle
        end if
      end tell
    end tell
EOF
